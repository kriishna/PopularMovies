apply plugin: 'com.android.application'

// versioning from stackoverflow and Jake Wharton
// https://plus.google.com/+JakeWharton/posts/6f5TcVPRZij
// http://stackoverflow.com/questions/4616095/how-to-get-the-build-version-number-of-your-android-application
def versionMajor = 1
def versionMinor = 1  //  constraint layout for movie detail now
def versionPatch = 7
def versionBuild = 17 // bump for dogfood builds, public betas, etc.

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    defaultConfig {
        applicationId "com.jimandreas.popularmovies"
        minSdkVersion 16
        targetSdkVersion 25

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }
    signingConfigs {
        release
    }
    buildTypes {
        buildTypes.each {
            it.buildConfigField 'String', 'THE_MOVIE_DATABASE_API_KEY', TheMovieDbApiToken
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

// signing solution courtesy of http://gist.github.com/gabrielemariotti/6856974

def Properties props = new Properties()
def propFile = new File('gradle/signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'gradle/signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'gradle/signing.properties not found'
    android.buildTypes.release.signingConfig = null
}

// see gradle.properties
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'

    // NOTE: switch between these two - the app compat was broken apart as of 24.2

    compile "com.android.support:appcompat-v7:${android_support_lib_version}"
    // compile "com.android.support:support-compat:${android_support_lib_version}"

    compile "com.android.support:design:${android_support_lib_version}"
    // compile "com.android.support:gridlayout-v7:${android_support_lib_version}"
    compile "com.android.support:cardview-v7:${android_support_lib_version}"
    compile "com.android.support:recyclerview-v7:${android_support_lib_version}"
    compile 'com.squareup.picasso:picasso:2.5.2'
    //compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-beta4'
    compile 'com.jakewharton.timber:timber:4.3.1'
}
